plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}

version = '1.20.1-7.0.7'
group = 'com.blakebr0.mysticalagriculture'

base {
    archivesName = 'MysticalAgriculture'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

boolean isWorkspace = findProject(':Cucumber') != null

repositories {
    maven { url 'https://maven.blakesmods.com' }
    maven { url 'https://maven.blamejared.com' }
    maven { url 'https://maven.tterrag.com' }
    maven {
        url 'https://cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

minecraft {
    mappings channel: 'official', version: '1.20.1'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    copyIdeResources = true

    runs {
        client {
            workingDirectory project.file('run')

            arg '-mixin.config=cucumber.mixins.json'

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            ideaModule rootProject.name != project.name ? "${rootProject.name}.${project.name}.main" : "${project.name}.main"

            mods {
                mysticalagriculture {
                    source sourceSets.main
                }

                if (isWorkspace) {
                    cucumber {
                        source project(':Cucumber').sourceSets.main
                    }
                }
            }
        }

        server {
            workingDirectory project.file('run')

            arg '-mixin.config=cucumber.mixins.json'

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            ideaModule rootProject.name != project.name ? "${rootProject.name}.${project.name}.main" : "${project.name}.main"

            mods {
                mysticalagriculture {
                    source sourceSets.main
                }

                if (isWorkspace) {
                    cucumber {
                        source project(':Cucumber').sourceSets.main
                    }
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            ideaModule rootProject.name != project.name ? "${rootProject.name}.${project.name}.main" : "${project.name}.main"

            args '--mod', 'mysticalagriculture', '--all', '--output', file('src/generated/resources/'), '--existing', sourceSets.main.resources.srcDirs[0]

            mods {
                mysticalagriculture {
                    source sourceSets.main
                }

                if (isWorkspace) {
                    cucumber {
                        source project(':Cucumber').sourceSets.main
                    }
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDirs 'src/generated/resources'
    exclude '.cache'
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"

    if (isWorkspace) {
        implementation(project(':Cucumber')) {
            transitive = false
        }
    } else {
        implementation fg.deobf("com.blakebr0.cucumber:Cucumber:${cucumber_version}")
    }

    implementation fg.deobf("curse.maven:jade-324717:${jade_version}")
    implementation fg.deobf("curse.maven:the-one-probe-245211:${top_version}")
    implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-forge-${crafttweaker_version}")

    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_mc_version}-forge:${jei_version}")

    compileOnly "mezz.jei:jei-${jei_mc_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${jei_mc_version}-forge-api:${jei_version}"
}

tasks.register('apiJar', Jar) {
    from(sourceSets.main.output) {
        include 'com/blakebr0/mysticalagriculture/api/**'
    }

    archiveClassifier = 'api'
}

artifacts {
    archives apiJar
}

jar {
    manifest {
        attributes([
            'Specification-Title': 'Mystical Agriculture',
            'Specification-Vendor': 'BlakeBr0',
            'Specification-Version': '1',
            'Implementation-Title': project.name,
            'Implementation-Version': "${version.split('-').last()}",
            'Implementation-Vendor' : 'BlakeBr0',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
            artifact apiJar
        }
    }

    repositories {
        maven {
            url = 'https://maven.blakesmods.com'

            credentials {
                username = System.getenv('BLAKESMODS_MAVEN_USERNAME')
                password = System.getenv('BLAKESMODS_MAVEN_PASSWORD')
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

tasks.register('addTestDatapacks', Copy) {
    if (file('run/saves').exists()) {
        file('run/saves').eachDir { dir ->
            project.copy {
                from 'src/test/resources/datapacks'
                into file(dir.path + '/datapacks')
            }
        }
    }

    project.copy {
        from 'src/test/resources/datapacks'
        into file('run/world/datapacks')
    }
}

tasks.register('addTestScripts', Copy) {
    from 'src/test/resources/scripts'
    into file('run/scripts')
}

tasks.processResources.finalizedBy('addTestDatapacks', 'addTestScripts')