buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'
apply plugin: 'maven-publish'

version = '1.16.5-4.2.6'
group = 'com.blakebr0.mysticalagriculture'
archivesBaseName = 'MysticalAgriculture'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

repositories {
    maven { url 'https://dvs1.progwml6.com/files/maven/' }
    maven { url 'https://maven.blamejared.com' }
    maven { url 'https://maven.tehnut.info' }
    maven { url "https://maven.tterrag.com" }

    flatDir { dir 'libs' }
}

minecraft {
    mappings channel: 'official', version: '1.16.5'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            ideaModule "${project.name}.main"
            environment 'MOD_CLASSES', "${project.file('out/production/resources').canonicalPath}${File.pathSeparator}${project.file('out/production/classes').canonicalPath}"
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            ideaModule "${project.name}.main"
            environment 'MOD_CLASSES', "${project.file('out/production/resources').canonicalPath}${File.pathSeparator}${project.file('out/production/classes').canonicalPath}"
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            ideaModule "${project.name}.main"
            environment 'MOD_CLASSES', "${project.file('out/production/resources').canonicalPath}${File.pathSeparator}${project.file('out/production/classes').canonicalPath}"

            args '--mod', 'mysticalagriculture', '--all', '--output', file('src/generated/resources/'), '--existing', sourceSets.main.resources.srcDirs[0]
        }
    }
}

sourceSets {
    main {
        resources {
            srcDirs 'src/generated/resources'
            exclude '.cache'
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.10'

    compile fg.deobf('ignored:Cucumber:1.16.4-4.1.10')
    compileOnly fg.deobf('mcp.mobius.waila:Hwyla:1.10.11-B78_1.16.2:api')
    runtimeOnly fg.deobf('mcp.mobius.waila:Hwyla:1.10.11-B78_1.16.2')
    compileOnly fg.deobf('mezz.jei:jei-1.16.5:7.7.0.98:api')
    runtimeOnly fg.deobf('mezz.jei:jei-1.16.5:7.7.0.98')
    compile fg.deobf('com.blamejared.crafttweaker:CraftTweaker-1.16.5:7.1.0.326')
    compile fg.deobf('vazkii.patchouli:Patchouli:1.16.2-47')
    compile fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.1.3-21") { transitive = false }
}

task apiJar(type: Jar) {
    // Remove sources from the api jar when MinecraftForge/ForgeGradle#369 is fixed
    from(sourceSets.main.allJava)
    from(sourceSets.main.output)
    include 'com/blakebr0/mysticalagriculture/api/**'

    classifier = 'api'
    archiveName = "${baseName}-${version}-api.${extension}"
}

artifacts {
    archives apiJar
}

jar {
    from sourceSets.main.output
    manifest {
        attributes([
            'Specification-Title': 'Mystical Agriculture',
            'Specification-Vendor': 'BlakeBr0',
            'Specification-Version': '1',
            'Implementation-Title': project.name,
            'Implementation-Version': "${version.split('-').last()}",
            'Implementation-Vendor' : 'BlakeBr0',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
            artifact apiJar
        }
    }

    repositories {
        maven {
            url = 'https://maven.blakesmods.com'

            credentials {
                username = System.getenv('BLAKESMODS_MAVEN_USERNAME')
                password = System.getenv('BLAKESMODS_MAVEN_PASSWORD')
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}