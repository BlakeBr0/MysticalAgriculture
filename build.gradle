buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

version = '2.0.3'
group = 'com.blakebr0.mysticalagriculture'
archivesBaseName = 'MysticalAgriculture-1.14.4'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

repositories {
    maven {
        url = "http://dvs1.progwml6.com/files/maven/"
    }

    maven {
        url = "http://tehnut.info/maven/"
    }

    mavenLocal()
    jcenter()
}

minecraft {
    mappings channel: 'snapshot', version: '20190803-1.14.3'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'

            ideaModule "${project.name}.main"
            environment 'MOD_CLASSES', "${project.file("out/production/resources").canonicalPath};${project.file("out/production/classes").canonicalPath}"
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'

            ideaModule "${project.name}.main"
            environment 'MOD_CLASSES', "${project.file("out/production/resources").canonicalPath};${project.file("out/production/classes").canonicalPath}"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.14.4-28.1.87'

    compile files('../Cucumber/build/libs/Cucumber-1.14.4-2.0.6-deobf.jar')
    compile fg.deobf('mcp.mobius.waila:Hwyla:1.10.5-B66_1.14.4')
    compileOnly fg.deobf("mezz.jei:jei-1.14.4:6.0.0.10:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.14.4:6.0.0.10")
}

task apiJar(type: Jar) {
    // Remove sources from the api jar when MinecraftForge/ForgeGradle#369 is fixed
    from(sourceSets.main.allJava)
    from(sourceSets.main.output)
    include 'com/blakebr0/mysticalagriculture/api/**'

    classifier = 'api'
    archiveName = "${baseName}-${version}-api.${extension}"
}

artifacts {
    archives apiJar
}

processResources {
    
    doLast {
    
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
        
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

jar {
    from sourceSets.main.output
    manifest {
        attributes([
            "Specification-Title": "Mystical Agriculture",
            "Specification-Vendor": "BlakeBr0",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"BlakeBr0",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}