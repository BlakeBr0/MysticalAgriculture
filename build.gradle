buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

version = '1.19.3-6.1.7'
group = 'com.blakebr0.mysticalagriculture'
archivesBaseName = 'MysticalAgriculture'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

boolean isWorkspace = findProject(':Cucumber') != null

repositories {
    maven { url 'https://maven.blakesmods.com' }
    maven { url 'https://maven.blamejared.com' }
    maven { url 'https://maven.tterrag.com' }
    maven {
        url 'https://cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

minecraft {
    mappings channel: 'official', version: '1.19.3'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            arg '-mixin.config=cucumber.mixins.json'

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            ideaModule rootProject.name != project.name ? "${rootProject.name}.${project.name}.main" : "${project.name}.main"

            mods {
                mysticalagriculture {
                    source sourceSets.main
                }

                if (isWorkspace) {
                    cucumber {
                        source project(':Cucumber').sourceSets.main
                    }
                }
            }
        }

        server {
            workingDirectory project.file('run')

            arg '-mixin.config=cucumber.mixins.json'

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            ideaModule rootProject.name != project.name ? "${rootProject.name}.${project.name}.main" : "${project.name}.main"

            mods {
                mysticalagriculture {
                    source sourceSets.main
                }

                if (isWorkspace) {
                    cucumber {
                        source project(':Cucumber').sourceSets.main
                    }
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            ideaModule rootProject.name != project.name ? "${rootProject.name}.${project.name}.main" : "${project.name}.main"

            args '--mod', 'mysticalagriculture', '--all', '--output', file('src/generated/resources/'), '--existing', sourceSets.main.resources.srcDirs[0]

            mods {
                mysticalagriculture {
                    source sourceSets.main
                }

                if (isWorkspace) {
                    cucumber {
                        source project(':Cucumber').sourceSets.main
                    }
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDirs 'src/generated/resources'
    exclude '.cache'
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"

    if (findProject(':Cucumber')) {
        implementation project(':Cucumber')
    } else {
        implementation fg.deobf("com.blakebr0.cucumber:Cucumber:${cucumber_version}")
    }

    implementation fg.deobf("curse.maven:jade-324717:${jade_version}")
    implementation fg.deobf("curse.maven:top-245211:${top_version}")
    implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-forge-${crafttweaker_version}")

    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_mc_version}-forge:${jei_version}")

    compileOnly "mezz.jei:jei-${jei_mc_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${jei_mc_version}-forge-api:${jei_version}"
}

task apiJar(type: Jar) {
    // Remove sources from the api jar when MinecraftForge/ForgeGradle#369 is fixed
    from(sourceSets.main.allJava)
    from(sourceSets.main.output)
    include 'com/blakebr0/mysticalagriculture/api/**'

    classifier = 'api'
    archiveName = "${baseName}-${version}-api.${extension}"
}

artifacts {
    archives apiJar
}

jar {
    manifest {
        attributes([
            'Specification-Title': 'Mystical Agriculture',
            'Specification-Vendor': 'BlakeBr0',
            'Specification-Version': '1',
            'Implementation-Title': project.name,
            'Implementation-Version': "${version.split('-').last()}",
            'Implementation-Vendor' : 'BlakeBr0',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
            artifact apiJar
        }
    }

    repositories {
        maven {
            url = 'https://maven.blakesmods.com'

            credentials {
                username = System.getenv('BLAKESMODS_MAVEN_USERNAME')
                password = System.getenv('BLAKESMODS_MAVEN_PASSWORD')
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

tasks.register('addTestDatapacks', Copy) {
    if (file('run/saves').exists()) {
        file('run/saves').eachDir { dir ->
            project.copy {
                from 'src/test/resources/datapacks'
                into file(dir.path + '/datapacks')
            }
        }
    }

    project.copy {
        from 'src/test/resources/datapacks'
        into file('run/world/datapacks')
    }
}

tasks.register('addTestScripts', Copy) {
    from 'src/test/resources/scripts'
    into file('run/scripts')
}

tasks.processResources.finalizedBy('addTestDatapacks', 'addTestScripts')